:const ninja_speed_left -2
:const ninja_speed_right 2
:const ninja_speed_down 4

: screen_pos
: screen_y 0
: screen_x 0

: ninja_current_animation_frame
	0

: ninja_current_pos
	16 40

: ninja_current_dir
	0
: ninja_jump_counter	#do not move it, current_dir reads it
	0
: ninja_falling			#do not move it, reading 3 registers at once
	1

:const ninja_jump_ticks 8


: ninja_jump_table
	0 5 5 4 2 -2 -4 -5 -5

: ninja_last_dir
	ninja_speed_right

: ninja_process_input
	v2 := 0
	v1 := 7		# A - left
	if v1 key then
		v2 += ninja_speed_left

	v1 := 9		# D - jump
	if v1 key then
		v2 += ninja_speed_right

	v1 := 5		# W - jump
	if v1 key begin
		i := ninja_jump_counter
		load v1 - v1
		if v1 == 0 begin
			v1 := ninja_jump_ticks
			save v1 - v1
		end
	end

	i := ninja_current_dir
	save v2 - v2
	return

: ninja_tick
	i := ninja_current_dir
	load vc - ve

	v0 := vc
	v0 |= vd
	v0 |= ve

	if v0 == 0 then
		return

	ninja_draw

	i := ninja_current_pos
	load va - vb

	#jump height
	if vd != 0 begin
		i := ninja_jump_table
		i += vd
		load ve - ve
		vd += -1
		i := ninja_jump_counter
		save vd - vd
	end

	if vd == 0 begin
		vb += 16 #square below
		map_is_wall

		if v0 != 0 then
			ve := 0

		if v0 == 0 then
			ve += ninja_speed_down

		i := ninja_falling
		save ve - ve
		vb += -16
	end


	va += vc
	vb += ve

	if vc != 0 begin
		i := ninja_last_dir
		save vc - vc
	end

	if vc == ninja_speed_right then
		va += 7
	if vc == ninja_speed_left then
		va += -1

	vb += 8
	map_is_wall
	vb += -8

	if vc == ninja_speed_right then
		va += -7
	if vc == ninja_speed_left then
		va += 1

	if v0 == 0 begin
		i := ninja_current_pos
		save va - vb

		if va >= 0x80 begin #negative
			map_scroll_left
			i := ninja_current_pos
			load va - vb
			va := 120
			save va - vb
			jump ninja_tick_exit
		end

		if va >= 120 begin
			map_scroll_right
			i := ninja_current_pos
			load va - vb
			va := ninja_speed_right # just a little gap
			save va - vb
			#jump ninja_tick_exit #fixme: reenable if not last statement
		end
	end

: ninja_tick_exit
	ninja_next_animation
	jump ninja_draw

: ninja_next_animation
	i := ninja_current_animation_frame
	load v0 - v0
	v0 += 1
	if v0 >= 4 then
		v0 := 0
	save v0 - v0
	return

: ninja_draw
	i := ninja_current_animation_frame
	load v0 - v0
	if v0 == 3 then
		v0 := 1

	v0 += v0
	v0 += v0
	v0 += v0
	v0 += v0

	i := ninja_current_pos
	load v1 - v2

	i := ninja_last_dir
	load v3 - v3

	v4 := 0
	if v3 > 128 then
		v4 := 48

	i := long tile_ninja_0_0
	i += v0
	i += v4
	sprite v1 v2 8

	i := long tile_ninja_1_0
	i += v0
	i += v4
	v2 += 8
	sprite v1 v2 8

	return
