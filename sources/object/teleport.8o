: object_teleport_draw
	i := long tile_teleport_0_0
	v2 := v1
	v2 <<= v2
	v2 <<= v2
	v2 <<= v2
	v2 <<= v2
	v2 <<= v2
	v2 <<= v2
	i += v2
	sprite vc vd 0
	return


: teleport_fall_start
	3 0 64 0
: teleport_training_start
	1 0 16 16
: teleport_invisible_walls
	5 7 16 40
: teleport_invisible_walls_return
	4 1 60 16
: teleport_puzzle_entrance
	6 7 32 40
: teleport_puzzle_exit
	4 2 60 8
: teleport_puzzle_2
	6 7 32 8
: teleport_puzzle_3
	7 7 60 40
: teleport_puzzle_win
	7 7 60 8
: teleport_cloud_treetop
	0 4 108 16
: teleport_final_puzzle
	2 0 60 40
: teleport_ending
	7 0 80 40

: object_teleport_collide
	if v1 == 0 then #disabled
		return

	i := long teleport_sound
	audio
	v0 := 10
	buzzer := v0

	if va == screen_1_0_teleport_0 then #return teleport from training camp
		jump ninja_respawn

	if va == screen_4_0_teleport_0 begin
		i := teleport_fall_start
		jump ninja_spawn
	end

	if va == screen_4_1_teleport_0 begin
		i := teleport_invisible_walls
		jump ninja_spawn
	end

	if va == screen_3_2_teleport_1 begin
		i := teleport_puzzle_exit
		jump ninja_spawn
	end

	if va == screen_3_2_teleport_0 begin
		i := teleport_puzzle_entrance
		jump ninja_spawn
	end

	if va == screen_4_3_teleport_0 begin
		i := teleport_training_start
		jump ninja_spawn
	end

	if va == screen_4_5_teleport_0 begin
		i := teleport_cloud_treetop
		jump ninja_spawn
	end

	if va == screen_6_7_teleport_2 begin #first teleport of teleport puzzle
		va := screen_6_7_teleport_0
		teleport_activate
		va := screen_6_7_teleport_1
		teleport_activate
		va := screen_6_7_teleport_2
		teleport_deactivate
		i := teleport_puzzle_entrance
		jump ninja_spawn
	end

	if va == screen_6_7_teleport_0 begin #left ground floor
		va := screen_6_7_teleport_3 #left first floor
		teleport_activate
		va := screen_6_7_teleport_5 #right first floor
		teleport_activate
		i := teleport_puzzle_2
		jump ninja_spawn
	end

	if va == screen_6_7_teleport_1 then
		jump teleport_puzzle_reset #right ground floor, wrong

	if va == screen_6_7_teleport_3 then #left first floor, wrong
		jump teleport_puzzle_reset

	if va == screen_6_7_teleport_5 begin #right first floor, wrong
		va := screen_6_7_teleport_3
		teleport_deactivate
		va := screen_6_7_teleport_5
		teleport_deactivate
		va := screen_6_7_teleport_4 # center, first floor
		teleport_activate
		i := teleport_puzzle_2
		jump ninja_spawn
	end

	if va == screen_6_7_teleport_4 begin
		va := screen_7_7_teleport_0
		teleport_activate
		va := screen_7_7_teleport_2
		teleport_activate
		i := teleport_puzzle_3
		jump ninja_spawn
	end

	if va == screen_7_7_teleport_2 then
		jump teleport_puzzle_reset #upper floor, left

	if va == screen_7_7_teleport_0 begin
		va := screen_7_7_teleport_1
		teleport_activate
		i := teleport_puzzle_win
		jump ninja_spawn
	end

	if va == screen_7_7_teleport_1 begin
		teleport_puzzle_reset_impl #no respawn
		ninja_respawn
	end

	:breakpoint teleport
	return

: teleport_puzzle_reset_impl
	va := screen_6_7_teleport_0
	teleport_deactivate
	va := screen_6_7_teleport_1
	teleport_deactivate
	va := screen_6_7_teleport_2
	teleport_activate

	va := screen_6_7_teleport_3
	teleport_deactivate
	va := screen_6_7_teleport_4
	teleport_deactivate
	va := screen_6_7_teleport_5
	teleport_deactivate

	va := screen_7_7_teleport_0
	teleport_deactivate
	va := screen_7_7_teleport_1
	teleport_deactivate
	va := screen_7_7_teleport_2
	jump teleport_deactivate

: teleport_puzzle_reset
	teleport_puzzle_reset_impl
	i := teleport_puzzle_entrance
	jump ninja_spawn

: object_teleport_tick
	object_teleport_draw
	object_teleport_storage_addr

	if v1 != 0 then
		v1 += 1

	if v1 >= 4 then
		v1 := 1
	save v1 - v1
	jump object_teleport_draw

: teleport_deactivate
	vb := 0
	jump teleport_set_activate

: teleport_activate
	vb := 1

: teleport_set_activate
	object_teleport_storage_addr
	save vb - vb
	return
