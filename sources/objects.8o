: objects_init
	va := screen_6_7_teleport_2
	teleport_activate
	va := screen_4_0_teleport_0
	teleport_activate
	return

: objects_tick
	map_get_screen_index
	i := long map_tick_objects_list
	i += v0
	load v0 - v1
	jump call_v0_v1

: objects_draw
	map_get_screen_index
	i := long map_draw_objects_list
	i += v0
	load v0 - v1
	jump call_v0_v1

: objects_collide
	map_get_screen_index

	i := long map_collide_objects_list
	i += v0
	load v0 - v1

	i := ninja_current_pos
	load va - vb
	jump call_v0_v1

: objects_screen_entered
	map_get_screen_index
	v0 >>= v0 #result is map_index * 2
	if v0 == 0 then
		draw_intro_1
	if v0 == 1 then
		draw_intro_2
	if v0 == 2 then
		draw_intro_3

	i := ninja_flappy_mode
	v1 := 0
	if v0 == 44 then #flappy screens
		v1 := 1
	if v0 == 45 then
		v1 := 1
	save v1 - v1

	jump objects_draw

: object_draw_tiles #v0/v1 - width/height
	v2 := 0
	v4 := vd #y
	v6 := 16
	loop
		v3 := 0
		v5 := vc #x
		loop
			v3 += 1
			v5 += 8
			sprite v5 v4 8
			i += v6
			if v3 != v0 then
		again

		v4 += 8
		v2 += 1
		if v2 != v1 then
	again
	return
