: object_teleport_draw
	i := long tile_teleport_0_0
	v2 := v1
	v2 <<= v2
	v2 <<= v2
	v2 <<= v2
	v2 <<= v2
	v2 <<= v2
	v2 <<= v2
	i += v2
	sprite vc vd 0
	return


: teleport_fall_start
	3 0 64 0
: teleport_training_start
	1 0 16 16
: teleport_invisible_walls
	5 7 16 40
: teleport_invisible_walls_return
	4 1 60 16
: teleport_puzzle_entrance
	4 2 60 8
: teleport_puzzle_exit
	6 7 32 16

: object_teleport_collide
	if v1 == 0 then #disabled
		return

	i := long teleport_sound
	audio
	v0 := 10
	buzzer := v0

	if va == screen_4_0_teleport_0 begin
		i := teleport_fall_start
		jump ninja_spawn
	end

	if va == screen_4_1_teleport_0 begin
		i := teleport_invisible_walls
		jump ninja_spawn
	end

	if va == screen_3_2_teleport_1 begin
		i := teleport_puzzle_exit
		jump ninja_spawn
	end

	if va == screen_3_2_teleport_0 begin
		i := teleport_puzzle_entrance
		jump ninja_spawn
	end

	if va == screen_4_3_teleport_0 begin
		i := teleport_training_start
		jump ninja_spawn
	end

	if va == screen_1_0_teleport_0 then
		jump ninja_respawn

	return

: object_teleport_tick
	object_teleport_draw
	object_teleport_storage_addr

	if v1 != 0 then
		v1 += 1

	if v1 >= 4 then
		v1 := 1
	save v1 - v1
	jump object_teleport_draw

: teleport_deactivate
	vb := 0
	jump teleport_set_activate

: teleport_activate
	vb := 1

: teleport_set_activate
	object_teleport_storage_addr
	save vb - vb
	return
