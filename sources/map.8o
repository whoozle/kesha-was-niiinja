:org 0x400

: render_line
	v0 := 1
	v1 := 16
	:next map_offset_increment
	v2 := 1
	loop
		0xf0 0x00
		: map_current_offset
		0x00 0x00 #map offset + starting offset

		load v4 - v4 #current tile

		i := map_current_offset
		load v5 - v6
		v6 += v2
		v5 += vf
		save v5 - v6

		if v4 == 0 then
			jump next_tile

		v4 += -1
		v3 := v4

		#hi of v3:v4 << 4
		v3 >>= v3
		v3 >>= v3
		v3 >>= v3
		v3 >>= v3

		v3 += 0x60 #tiles hi offset

		#lo of v3:v4 << 4
		v4 <<= v4
		v4 <<= v4
		v4 <<= v4
		v4 <<= v4

		i := current_tile_offset
		save v3 - v4

		0xf0 0x00
		: current_tile_offset
		00 00

		sprite va vb 8

	: next_tile
		:next map_line_dx
		va += 8

		:next map_line_dy
		vb += 0

		v1 += -1
		if v1 != 0 then
	again

	return

: render_screen
	i := map_current_offset
	save va - vb

	v0 := 8
	i := map_line_dx
	save v0 - v0

	v0 := 0
	i := map_line_dy
	save v0 - v0

	v0 := 1
	i := map_offset_increment
	save v0 - v0

	vc := 0
	v7 := 0x70 # map_width - 16

	loop
		va := 0
		vb := ve
		render_line

		i := map_current_offset
		load v0 - v1
		v1 += v7
		v0 += vf
		save v0 - v1

		vc += 8

		if vc < 64 then
	again

	return
